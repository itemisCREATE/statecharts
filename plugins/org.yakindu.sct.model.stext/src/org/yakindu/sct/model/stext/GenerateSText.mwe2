module org.yakindu.sct.model.stext.Stext

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var fileExtensions = "stext"
var projectName = "org.yakindu.sct.model.stext"
var runtimeProject = "../${projectName}"
var rootPath = ".."

Workflow {
	bean = StandaloneSetup {
		scanClassPath = true
		platformUri = "${runtimeProject}/.."
		registerGenModelFile = "platform:/resource/org.yakindu.sct.model.sgraph/model/emf/sgraph.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.sct.model.stext/model/SText.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.base.types/model/base.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.base.types/model/types.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.base.expressions/model/Expressions.genmodel"
		registerEcoreFile = "platform:/resource/org.yakindu.base.types/model/base.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.base.types/model/types.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.base.expressions/model/Expressions.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.sct.model.sgraph/model/emf/sgraph.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.sct.model.stext/model/SText.ecore"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
				genericIde = {
					name = "${projectName}.ui"
					srcGen = "${rootPath}/${projectName}.ui/src-gen"
					enabled = true
					manifest = {
						merge = false
					}
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * (c) by committers of YAKINDU */"
			}
		}
		language = StandardLanguage {
			name = "org.yakindu.sct.model.stext.SText"
			fileExtensions = fileExtensions
			serializer = {
				generateStub = false
			}
			generator = {
				generateStub = false
			}
			parserGenerator = {
				options = {
					skipUnusedRules = true
				}
			}
		}
	}
}
